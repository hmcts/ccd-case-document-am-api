buildscript {
  repositories {
    jcenter()
  }
  configurations.maybeCreate("pitest")
  dependencies {
    classpath("net.serenity-bdd:serenity-gradle-plugin:2.2.8")
    classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.5.0'
    pitest 'org.pitest:pitest-junit5-plugin:0.12'
  }
}

plugins {
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.0.9.RELEASE'
  id 'info.solidsoft.pitest' version '1.5.0'
  id 'org.springframework.boot' version '2.2.7.RELEASE'
  id 'org.owasp.dependencycheck' version '5.3.2.1'
  id 'com.github.ben-manes.versions' version '0.28.0'
  id 'org.sonarqube' version '2.8'
  id 'com.github.spacialcircumstances.gradle-cucumber-reporting' version '0.1.21'
}

apply plugin: "info.solidsoft.pitest"
apply plugin: 'java'
apply plugin: 'net.serenity-bdd.aggregator'
apply plugin: 'project-report'
apply plugin: 'idea'
apply plugin: 'io.spring.dependency-management'

def versions = [
  junit              : '5.5.0',
  lombok             : '1.18.6',
  gradlePitest       : '1.3.0',
  pitest             : '1.4.2',
  reformHealthStarter: '0.0.5',
  reformLogging      : '5.1.1',
  reformS2sClient    : '3.0.0',
  serenity           : '2.1.0',
  sonarPitest        : '0.5',
  springBoot         : '2.1.4.RELEASE',
  springHystrix      : '2.1.1.RELEASE',
  swagger2Version    : '3.0.0-SNAPSHOT',
  pact_version       : '3.5.24',
  rest_assured       : '3.3.0',
  cucumber           : '5.5.0',
  feign_jackson      : '9.7.0'
]

mainClassName = 'uk.gov.hmcts.reform.ccd.documentam.Application'
def jarName = 'ccd-case-document-am-api.jar'

pmd {
  toolVersion = '6.21.0'
  sourceSets = []
}

group = 'uk.gov.hmcts.reform.ccd.documentam'
version = '0.0.1'

allprojects {
  sourceCompatibility = '1.8'
  targetCompatibility = '1.8'
}

jacocoTestCoverageVerification {
  violationRules {
    rule {
      limit {
        counter = 'LINE'
        value = 'COVEREDRATIO'
        minimum = 0.0
      }
    }
  }
}

configurations {
  functionalTestCompile.extendsFrom testCompile
  functionalTestRuntime.extendsFrom testRuntime
  cucumberRuntime.extendsFrom(functionalRuntime)
}

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  integrationTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
  doFirst {
    generateCucumberReports.enabled = true
    javaexec {
      main = "uk.gov.hmcts.ccd.documentam.befta.CaseDocumentAmBeftaMain"
      classpath += configurations.cucumberRuntime + sourceSets.functionalTest.runtimeClasspath + sourceSets.main.output + sourceSets.test.output
      args = ['--plugin', "json:${projectDir}/target/cucumber.json", '--tags', 'not @Ignore', '--glue',
              'uk.gov.hmcts.befta.player', 'src/functionalTest/resources/features/']
    }
  }
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  serenity.outputDirectory = file("${projectDir}/integrationTest/site/reports")
  finalizedBy aggregate
}

task smoke(type: Test, description: 'Runs the smoke tests.', group: 'Verification') {
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
  finalizedBy aggregate
}

checkstyle {
  maxWarnings = 0
  toolVersion = '8.18'
  // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
  configDir = new File(rootDir, 'config/checkstyle')

  checkstyleMain.shouldRunAfter(compileJava)
  test.shouldRunAfter(checkstyleTest)
}

jacocoTestReport {
  executionData(test)
  reports {
    xml.enabled = true
    csv.enabled = false
    xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

jacoco {
  toolVersion = "0.8.2"
}

pitest {
  pitestVersion = "1.4.9"
  testPlugin = "junit5"
  targetClasses = ['uk.gov.hmcts.reform.ccd.documentam.*']
  excludedClasses = ['uk.gov.hmcts.reform.ccd.documentam.Application','uk.gov.hmcts.reform.ccd.documentam.util.Swagger2SpringBoot', 'uk.gov.hmcts.reform.ccd.documentam.controller.advice.ErrorConstants', 'uk.gov.hmcts.reform.ccd.documentam.configuration.*','uk.gov.hmcts.reform.ccd.documentam.apihelper.*', 'uk.gov.hmcts.reform.ccd.documentam.model.enums.*', 'uk.gov.hmcts.reform.ccd.documentam.exception.*']
  threads = 15
  outputFormats = ['XML', 'HTML']
  timestampedReports = true
  mutationThreshold = 40
}

project.tasks['pitest'].group="verification"

sonarqube {
  properties {
    property "sonar.projectName", "ccd-case-document-am-api"
    property "sonar.projectKey", "ccd-case-document-am-api"
    property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    property "sonar.pitest.mode", "reuseReport"
    property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    property "sonar.exclusions", "src/main/java/uk/gov/hmcts/reform/ccd/document/am/*Application.java," + "src/main/java/uk/gov/hmcts/reform/ccd/document/am/util/*Swagger2SpringBoot.java," + "src/main/java/uk/gov/hmcts/reform/ccd/document/am/controller/advice/*ErrorConstants.java," + "src/main/java/uk/gov/hmcts/reform/ccd/document/am/controller/advice/exception/**," + "src/main/java/uk/gov/hmcts/reform/ccd/document/am/configuration/**," + "src/main/java/uk/gov/hmcts/reform/ccd/document/am/model/enums/**," + "src/main/java/uk/gov/hmcts/reform/ccd/document/am/apihelper/**"
    property "sonar.junit.reportPaths", "${project.buildDir}/test-results/junit-platform"
  }
}

project.tasks['sonarqube'].dependsOn test, integration, jacocoTestReport
project.tasks['sonarqube'].group = "Verification"

dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all {
      def isNonStable = { String version ->
        ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
          version ==~ /(?i).*[.-]\$qualifier[.\\d-+]*/
        }
      }
      if (isNonStable(candidate.version) && !isNonStable(currentVersion)) {
        selection.reject('Release candidate')
      }
    }
  }
}

//https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFiles = ['config/owasp/suppressions.xml']
}

dependencyUpdates.resolutionStrategy = {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

repositories {
  mavenLocal()
  jcenter()
  mavenCentral()
  maven {
    url "https://dl.bintray.com/hmcts/hmcts-maven"
  }
  maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local/' }
}


dependencies {
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  compile group: 'uk.gov.hmcts.reform', name: 'health-spring-boot-starter', version: '0.0.5'
  compile group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'
  compile group: 'org.json', name: 'json', version: '20200518'

  //Added for Spring Security Implementation
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: versions.springBoot

  //Added for S2S and User Authentication
  compile group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: versions.reformS2sClient
  compile(group: 'uk.gov.hmcts.reform.auth', name: 'auth-checker-lib', version: '2.1.4') {
    // TODO remove when auth-checker-lib upgrades its dependency to spring boot 2.0.x
    exclude group: 'uk.gov.hmcts.reform', module: 'java-logging-spring'

  }
  compile group: 'javax.inject', name: 'javax.inject', version: '1'
  compile group: 'io.springfox', name: 'springfox-swagger2', version: versions.swagger2Version
  compile group: 'io.springfox', name: 'springfox-swagger-ui', version: versions.swagger2Version
  compile group: 'io.springfox', name: 'springfox-spring-webmvc', version: versions.swagger2Version

  compile group: 'uk.gov.hmcts.reform', name: 'logging', version: versions.reformLogging
  compile group: 'uk.gov.hmcts.reform', name: 'logging-appinsights', version: versions.reformLogging
  compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-netflix-hystrix', version: '2.2.2.RELEASE'
  compile('org.springframework.boot:spring-boot-starter-hateoas')
  compile group: 'org.springframework', name: 'spring-tx', version: '5.2.6.RELEASE'

  testCompile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'

  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
  testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junit
  testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit

  testCompile group: 'org.pitest', name: 'pitest', version: versions.pitest
  testCompile 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.4.7'
  testCompile 'org.codehaus.sonar-plugins:sonar-pitest-plugin:0.5'

  testCompile (group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot) {
    exclude group: 'junit', module: 'junit'
  }
  testCompile group: 'uk.gov.hmcts', name: 'befta-fw', version: '3.0.0'
  compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '2.2.2.RELEASE'

  // AM-467-Serenety report for Integration test
 integrationTestCompile group: 'io.rest-assured', name: 'rest-assured', version: versions.rest_assured

  integrationTestCompile sourceSets.main.runtimeClasspath
  integrationTestCompile sourceSets.test.runtimeClasspath

  functionalTestCompile sourceSets.main.runtimeClasspath
  functionalTestCompile sourceSets.test.runtimeClasspath

  compileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  smokeTestCompile sourceSets.main.runtimeClasspath
  smokeTestCompile sourceSets.test.runtimeClasspath
  smokeTestCompileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  smokeTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok

}


test {
  generateCucumberReports.enabled = false
  useJUnitPlatform()
  failFast = true
}


bootJar {
  archiveName = jarName

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

cucumberReports {
  outputDir = file("${projectDir}/target/cucumber")
  reports = files("${projectDir}/target/cucumber.json")
}
